PROGRAM _CYCLIC
	(* cyclic program *)

;Sequence 7 - Discharger 2 D[907]

;-----------------------------------------
;Revision 1.00 19/12/2005 D.J. First Issue
;-----------------------------------------

DischargerEnable 		= ((D[13] > 0) AND NOT Std.HaltSys)

;I_BagAtBuffer			= X[6].X07
I_OpenBag				= CAN[2].DropLoad OR TestDrop	
I_LoadDropped			= CAN[2].LoadDropped OR TestDropped	
I_BagAtBuffer			= X[6].X06 OR TestBuffer
I_BagAtDischarger		= X[6].X08 OR TestAtDisch
I_BagEmpty			  	= TRUE



I_BagAtEmptyBuffer		= FALSE ;X[6].X13
O_EmptyBagBufferOpen	= FALSE ;Y[6].Y13

O_BagReady				= FALSE
O_OpenBuffer 			= FALSE
O_Steadys				= FALSE
O_Closer				= FALSE
O_OpenBag				= FALSE
O_OpenStop				= FALSE

Request.DC[2] 			= FALSE
Request.DCEmptyRelease[2] = FALSE
;Alarm
Alarm[7].Name 			= almNoAlarm ;Clear sequence alarm name
Alarm[7].Bag 			= 188
Alarm[7].Beacon			= TRUE
Alarm[7].Extra			= 0
Alarm[7].Time			= 3000

;Setup Sequence
STEP = D[907]
;Setup Pulse contact
PLS = (STEP <> LASTSTEP)
LASTSTEP = STEP

;Step 0 - Disabled
IF (STEP = 0) THEN
	;Disabled
ENDIF

;Check Status - Bag settled at buffer, no bag at discharger, bag at buffer on screen and no bag at discharger on the screen
TON_10ms(I_BagAtBuffer,300,SettledAtBuffer,SettledAtBufferET)
IF (STEP = 1) THEN
	IF (NOT I_BagAtDischarger AND  (Bag[188].Cat = 0) AND NOT Std.HaltSys AND DischargerEnable AND SettledAtBuffer) THEN
		Request.DC[2] = TRUE
		D[907] = 2
	ENDIF
	IF I_BagAtDischarger THEN
		;D[907] = 2
	ENDIF
	IF (Bag[188].Cat > 0) THEN
		Alarm[7].Name = almBagDataError
	ELSE IF I_BagAtDischarger THEN
		Alarm[7].Name = almSwitchFault
	ELSE IF (Bag[188].Cat > 0) AND NOT I_BagAtBuffer THEN
		Alarm[7].Name 	= almBagNotAtProx
		Alarm[7].Bag	= 188
	ELSE IF (Bag[188].Cat = 0) AND I_BagAtBuffer AND DischargerEnable  THEN
		Alarm[7].Name 	= almBagNotShownOnScreen
		Alarm[7].Bag	= 188
	ENDIF
ENDIF

;Wait for bag in discharger
IF (STEP = 2) THEN
	O_OpenBuffer 	= TRUE
	IF I_BagAtDischarger THEN
		D[907] = 3
	ENDIF
	Alarm[7].Name = almNoAlarm ;almBagNotArrived
ENDIF

;Let bag settle in discharger
TON_10ms(((STEP = 3) AND I_BagAtDischarger),1000,SettledAtDischarger,SettledAtDischargerET)
IF (STEP = 3) THEN
	IF (Bag[188].Cat = 0) AND (Bag[189].Cat > 0) THEN
		Bag[188] = Bag[189]
		Bag[189] = Bag[0]
	ENDIF
	IF SettledAtDischarger THEN
		D[907] = 4
	ENDIF
	Alarm[7].Name = almBagNotSettled
ENDIF

;Allow steadys to move down
TON_10ms((STEP = 4),400,SteadysDown,SteadysDownET)
IF (STEP = 4) THEN
	IF SteadysDown THEN
		D[907] = 5
	ENDIF
	IF (Bag[188].Cat = Std.MaxCats) THEN
		D[907] = 8
	ENDIF
ENDIF

;Steady cylinders on from steps 4 - 9
O_Steadys = ((STEP >= 4) AND (STEP <= 9))

;Bag opener on from step 5 - 8
O_OpenBag = ((STEP >= 6) AND (STEP <= 8))

CodeStep  = ((STEP >= 4) AND (STEP <= 5))

;Wash Codes
IF CodeStep THEN

	Weight = Bag[188].Wgt
	
	;round up weight to give to wash machine
	Remainder = SINT(Bag[188].Wgt MOD 10)
	IF (Remainder > 4) THEN
		WgtRound = (Weight + 10)
	ELSE
		WgtRound = Weight
	ENDIF
	
	;WashCodes 
	;if Below the Machine limit of 99 and no WashCode has been entered then send the Category 
	
	
	CAN[2].Category 	= Bag[188].Cat
	CAN[2].Customer 	= Bag[188].Cus
	CAN[2].Weight   	= (WgtRound / 10) ;rounded up weight divide by 10
	;CAN[1].DoubleLoad 	= (Batch > 0)
ELSE 
	;CAN[2].Category 	= 0
	;CAN[2].Customer 	= 0
	;CAN[2].Weight   	= 0
	;CAN[1].DoubleLoad 	= FALSE

ENDIF

;Bag Ready to Washing Machine
IF (STEP = 5) THEN
	CAN[2].BagReady 		= TRUE
	
	IF I_OpenBag OR TestDischarger THEN
		D[907] = 6
	ENDIF
ELSE
	CAN[2].BagReady = FALSE
ENDIF

;Set LoadDropped to False
IF (STEP <> 6) THEN
	LoadDropped = FALSE
ENDIF

;Open Bag Until LoadDropped recieved and has been open for 6 seconds
TON_10ms((STEP = 6),600,MinOpen,MinOpenET) ;ET means Elapsed Time
TON_10ms((STEP = 6) AND NOT I_DischargerBagEmpty ,100,BagEmpty,BagEmptyET)

IF (STEP = 6) THEN
		
	;IF I_LoadDropped  OR TestDischarger THEN
	
		LoadDropped = TRUE
	;ENDIF

	IF (LoadDropped AND MinOpen) THEN
		;Write Report
		;------------

	ENDIF
	IF (LoadDropped AND MinOpen ) THEN
		IF (ReportUnloading[2].Cat = 0) THEN
			ReportUnloading[2].Cat = UINT(Bag[188].Cat)	;Category
			ReportUnloading[2].Cus = Bag[188].Cus		;Customer
			ReportUnloading[2].Wgt = Bag[188].Wgt		;Weight
			ReportUnloading[2].Cnt = 0					;Count
			ReportUnloading[2].Stn = 0					;Station
			ReportUnloading[2].Mcn = 2					;Machine
		ENDIF
		D[907] = 7
	ENDIF
	Alarm[7].Name = almLoadNotDropped
	ELSE
	LoadDropped = FALSE
ENDIF

;Change display to empty bag
IF (STEP = 7) THEN
	IF ((NOT I_OpenBag  AND I_BagEmpty) OR TestDrop) THEN
		Bag[188].Prg 	= Std.MaxProg
		Bag[188].Cat 	= Std.MaxCats
		Bag[188].Cus 	= Std.MaxCust

		D[907] 			= 8
	ENDIF
	Alarm[7].Name = almGeneralSequence
ENDIF

;Close bag
TON_10ms((STEP = 8),700,CloserDown,CloserDownET)
IF (STEP = 8) THEN
	O_Closer = TRUE
	IF CloserDown THEN
		D[907] = 9
	ENDIF
ENDIF

;Allow closer to go back up
TON_10ms((STEP = 9),700,CloserUp,CloserUpET)
IF (STEP = 9) THEN
	IF CloserUp THEN
		D[907] = 10
	ENDIF
ENDIF

;Allow steadys to go up
TON_10ms((STEP = 10),400,SteadyUp,SteadyUpET)
IF (STEP = 10) THEN
	IF SteadyUp THEN
		D[907] = 11
	ENDIF
ENDIF

;Request empty bag release
IF (STEP = 11) THEN
	Request.DCEmptyRelease[2] = TRUE
	IF Enable.DC[2] THEN 
		;C[32]  = 0
		D[907] = 12
	ENDIF
ENDIF

;Open buffer stop - unitl bag out
IF (STEP = 12) THEN
	O_OpenStop = TRUE
	Bag[188].Cat = 0
	Bag[188].Cus = 0
	Bag[188].Wgt = 0
	IF NOT Enable.DC[2] THEN
		D[907] = 1
	ENDIF
	Alarm[7].Name = almBagNotReleased
ENDIF

;Alarms
TimerAlarm.IN 	= NOT PLS AND (Alarm[7].Name > 0)
TimerAlarm.PT 	= Alarm[7].Time
TimerAlarm FUB TON_10ms()
Alarm[7].On 	= TimerAlarm.Q
Alarm[7].ET 	= TimerAlarm.ET

;Outputs
Y[6].Y06 = O_OpenBuffer
Y[6].Y07 = O_Steadys
Y[6].Y08 = O_OpenBag
Y[6].Y09 = O_OpenStop
Y[6].Y10 = O_Closer
END_PROGRAM

