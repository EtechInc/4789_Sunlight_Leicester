PROGRAM _CYCLIC
	(* cyclic program *)

;Sequence 7 - Discharger 2 D[907]

;-----------------------------------------
;Revision 1.00 19/12/2005 D.J. First Issue
;-----------------------------------------

DischargerEnable 		= ((D[15] > 0) AND NOT Std.HaltSys)

;I_BagAtBuffer			= X[6].X07
I_OpenBag				= X[1].X05              ;CAN[2].DropLoad OR TestDrop	
I_LoadDropped			= X[1].X06             ;CAN[2].LoadDropped OR TestDropped	
I_BagAtBuffer			= X[1].X02 OR TestBuffer
I_BagAtDischarger		= X[1].X03 OR TestAtDisch
I_BagEmpty			  	= TRUE



I_BagAtEmptyBuffer		= FALSE ;X[6].X13
O_EmptyBagBufferOpen	= FALSE ;Y[6].Y13

O_BagReady				= FALSE
O_OpenBuffer 			= FALSE
O_Steadys				= FALSE
O_Closer				= FALSE
O_OpenBag				= FALSE
O_OpenStop				= FALSE


;Alarm
Alarm[10].Name 			= almNoAlarm ;Clear sequence alarm name
Alarm[10].Bag 			= 80
Alarm[10].Beacon		= TRUE
Alarm[10].Extra			= 0
Alarm[10].Time			= 3000

;Setup Sequence
STEP = D[910]
;Setup Pulse contact
PLS = (STEP <> LASTSTEP)
LASTSTEP = STEP

;Step 0 - Disabled
IF (STEP = 0) THEN
	;Disabled
ENDIF

;Check Status - Bag settled at buffer, no bag at discharger, bag at buffer on screen and no bag at discharger on the screen
TON_10ms(I_BagAtBuffer,300,SettledAtBuffer,SettledAtBufferET)
IF (STEP = 1) THEN
	IF (NOT I_BagAtDischarger AND  (Bag[80].Cat = 0) AND NOT Std.HaltSys AND DischargerEnable AND SettledAtBuffer) THEN
		Request.DC[2] = TRUE
		D[910] = 2
	ENDIF
	IF I_BagAtDischarger THEN
		;D[907] = 2
	ENDIF
	IF (Bag[80].Cat > 0) THEN
		Alarm[10].Name = almBagDataError
	ELSE IF I_BagAtDischarger THEN
		Alarm[10].Name = almSwitchFault
	ELSE IF (Bag[80].Cat > 0) AND NOT I_BagAtBuffer THEN
		Alarm[10].Name 	= almBagNotAtProx
		Alarm[10].Bag	= 80
	ELSE IF (Bag[80].Cat = 0) AND I_BagAtBuffer AND DischargerEnable  THEN
		Alarm[10].Name 	= almBagNotShownOnScreen
		Alarm[10].Bag	= 80
	ENDIF
ENDIF

;Wait for bag in discharger
IF (STEP = 2) THEN
	O_OpenBuffer 	= TRUE
	IF I_BagAtDischarger THEN
		D[910] = 3
	ENDIF
	Alarm[10].Name = almNoAlarm ;almBagNotArrived
ENDIF

;Let bag settle in discharger
TON_10ms(((STEP = 3) AND I_BagAtDischarger),1000,SettledAtDischarger,SettledAtDischargerET)
IF (STEP = 3) THEN
	IF (Bag[80].Cat = 0) AND (Bag[81].Cat > 0) THEN
		Bag[80] = Bag[81]
		Bag[81] = Bag[0]
	ENDIF
	IF SettledAtDischarger THEN
		D[910] = 4
	ENDIF
	Alarm[10].Name = almBagNotSettled
ENDIF

;Allow steadys to move down
TON_10ms((STEP = 4),400,SteadysDown,SteadysDownET)
IF (STEP = 4) THEN
	IF SteadysDown THEN
		D[910] = 5
	ENDIF
	IF (Bag[80].Cat = Std.MaxCats) THEN
		D[910] = 8
	ENDIF
ENDIF

;Steady cylinders on from steps 4 - 9
O_Steadys = ((STEP >= 4) AND (STEP <= 9))

;Bag opener on from step 5 - 8
O_OpenBag = ((STEP >= 6) AND (STEP <= 8))

CodeStep  = ((STEP >= 4) AND (STEP <= 5))

;Wash Codes
IF CodeStep THEN

	Weight = Bag[80].Wgt
	
	;round up weight to give to wash machine
	Remainder = SINT(Bag[80].Wgt MOD 10)
	IF (Remainder > 4) THEN
		WgtRound = (Weight + 10)
	ELSE
		WgtRound = Weight
	ENDIF
ELSE 
ENDIF

;Bag Ready to Washing Machine
IF (STEP = 5) THEN
	O_BagReady = TRUE
	
	IF I_OpenBag OR TestDischarger THEN
		D[910] = 6
	ENDIF
ELSE

ENDIF

;Set LoadDropped to False
IF (STEP <> 6) THEN
	LoadDropped = FALSE
ENDIF

;Open Bag Until LoadDropped recieved and has been open for 6 seconds
TON_10ms((STEP = 6),600,MinOpen,MinOpenET) ;ET means Elapsed Time
TON_10ms((STEP = 6) AND NOT I_DischargerBagEmpty ,100,BagEmpty,BagEmptyET)

IF (STEP = 6) THEN
		
	;IF I_LoadDropped  OR TestDischarger THEN
	
		LoadDropped = TRUE
	;ENDIF

	IF (LoadDropped AND MinOpen) THEN
		;Write Report
		;------------

	ENDIF
	IF (LoadDropped AND MinOpen ) THEN
		IF (ReportUnloading[5].Cat = 0) THEN
			ReportUnloading[5].Cat = UINT(Bag[80].Cat)	;Category
			ReportUnloading[5].Cus = Bag[80].Cus		;Customer
			ReportUnloading[5].Wgt = Bag[80].Wgt		;Weight
			ReportUnloading[5].Cnt = 0					;Count
			ReportUnloading[5].Stn = 0					;Station
			ReportUnloading[5].Mcn = 5					;Machine
		ENDIF
		D[910] = 7
	ENDIF
	Alarm[10].Time			= 600
	Alarm[10].Name = almLoadNotDropped
	ELSE
	LoadDropped = FALSE
ENDIF

;Change display to empty bag
IF (STEP = 7) THEN
	IF ((NOT I_OpenBag  AND I_BagEmpty) OR TestDrop) THEN
		Bag[80].Prg 	= Std.MaxProg
		Bag[80].Cat 	= Std.MaxCats
		Bag[80].Cus 	= Std.MaxCust

		D[910] 			= 8
	ENDIF
	Alarm[10].Name = almGeneralSequence
ENDIF


;Allow steadys to go up
TON_10ms((STEP = 8),400,SteadyUp,SteadyUpET)
IF (STEP = 8) THEN
	IF SteadyUp THEN
		D[910] = 9
	ENDIF
ENDIF

;Request empty bag release
IF (STEP = 9) THEN
	
	IF Line[32].Clear THEN 
		C[32]  = 0
		D[910] = 10
	ENDIF
ENDIF

;Open buffer stop - unitl bag out
IF (STEP = 10) THEN
	O_OpenStop = TRUE
	Bag[80].Cat = 0
	Bag[80].Cus = 0
	Bag[80].Wgt = 0
	IF Line[32].Trip THEN
		D[910] = 1
	ENDIF
	Alarm[10].Name = almBagNotReleased
ENDIF

;Alarms
TimerAlarm.IN 	= NOT PLS AND (Alarm[10].Name > 0)
TimerAlarm.PT 	= Alarm[10].Time
TimerAlarm FUB TON_10ms()
Alarm[10].On 	= TimerAlarm.Q
Alarm[10].ET 	= TimerAlarm.ET

;Outputs
Y[1].Y02 = O_OpenBuffer
Y[1].Y04= O_Steadys
Y[1].Y06 = O_OpenBag
Y[1].Y03 = O_OpenStop
Y[1].Y05 = O_BagReady
END_PROGRAM

